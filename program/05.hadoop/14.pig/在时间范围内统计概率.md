http://www.dataguru.cn/thread-225913-1-1.html

```sh
### 1. 下载jar
cd /opt/pig-0.17.0/contrib/piggybank/java/lib
curl -O http://central.maven.org/maven2/joda-time/joda-time/1.6.2/joda-time-1.6.2.jar
curl -O http://central.maven.org/maven2/joda-time/joda-time/2.0/joda-time-2.0.jar
curl -O http://central.maven.org/maven2/com/linkedin/datafu/datafu/1.2.0/datafu-1.2.0.jar

### 2. 上传data.txt
hadoop fs -mkdir /opt        
hadoop fs -put data.txt /opt/
```


使用的数据文件如下：
002|2013-09-10 00-09|zhongshandaxue
002|2013-09-10 09-17|zhujiangxincheng
001|2013-09-12 00-09|zhongshandaxue
001|2013-09-12 09-17|guangzhouxinxigang
002|2013-09-13 00-09|zhongshandaxue
002|2013-09-13 09-17|guangzhouxinxigang
003|2013-09-14 00-09|zhongshandaxue
003|2013-09-14 09-17|guangzhouxinxigang
003|2013-09-14 17-24|tiyuzhongxin
001|2013-10-10 00-09|zhongshandaxue
001|2013-10-10 09-17|zhujiangxincheng


就是在一个时间范围内统计概率，修改了程序的源码如下,红色的部分为修改的代码：
```groovy
data = load '/opt/data.txt' USING PigStorage('|') AS (imsi:chararray,time:chararray,loc:chararray);
REGISTER /opt/pig-0.17.0/contrib/piggybank/java/piggybank.jar;
REGISTER /opt/pig-0.17.0/contrib/piggybank/java/lib/joda-time-2.0.jar
REGISTER /opt/pig-0.17.0/contrib/piggybank/java/lib/datafu-1.2.0.jar;

DEFINE CustomFormatToISO org.apache.pig.piggybank.evaluation.datetime.convert.CustomFormatToISO();                        
toISO = FOREACH data GENERATE imsi,CustomFormatToISO(SUBSTRING(time,0,13),'YYYY-MM-DD HH') AS time:chararray,loc;
grp = GROUP toISO  BY imsi;
                                                                                         
DEFINE MarkovPairs datafu.pig.stats.MarkovPairs();
pairs = FOREACH grp
{
sorted = ORDER toISO BY time;
pair = MarkovPairs(sorted);
GENERATE FLATTEN(pair) AS ( data:tuple(imsi,time,loc),next:tuple(imsi,time,loc));
}
prj = FOREACH pairs GENERATE data.imsi AS imsi,CONCAT(SUBSTRING(data.time,11,13),SUBSTRING(next.time,10,13)) AS timerange,data.time AS time,next.time AS next_time,data.loc AS loc,next.loc AS next_loc;
DEFINE ISODaysBetween org.apache.pig.piggybank.evaluation.datetime.diff.ISODaysBetween();
flt = FILTER prj BY ISODaysBetween(next_time,time) == 0L;
total_cnt = FOREACH (GROUP flt BY (timerange,loc)) GENERATE  FLATTEN(group) AS (timerange,loc),COUNT(flt) AS total;
pairs_count = FOREACH (GROUP flt by (timerange,loc,next_loc)) GENERATE FLATTEN(group) AS (timerange,loc,next_loc),COUNT(flt) AS cnt;
jnd = JOIN pairs_count BY (timerange,loc),total_cnt BY (timerange,loc) USING 'replicated';
prob = FOREACH jnd GENERATE pairs_count::timerange AS timerange,pairs_count::loc AS loc,pairs_count::next_loc AS next_loc,(double)cnt / (double)total AS probability;
top3 = FOREACH (GROUP prob by (timerange,loc))
{
sorted = ORDER prob BY probability DESC;
top = LIMIT sorted 3;
GENERATE FLATTEN(top);
}
describe top3;
dump top3;
// (00T09,zhongshandaxue,guangzhouxinxigang,0.6)
// (00T09,zhongshandaxue,zhujiangxincheng,0.4)
// (09T17,guangzhouxinxigang,tiyuzhongxin,1.0)
```


结果截图：
  

2（可选） 因为上一节课程的输出结果是保留最长停留的三个基站, 请编写一个 Pig 脚本, 用上一节最后一题的输出, 计算最长停留时间的一个. 

原始数据截图：
  

思路就是上一题中获得较大三个概率的方法
代码如下：
data = load '/user/flp/output/part-r-00000' USING PigStorage('|') AS (imsi:chararray,loc:chararray,time:chararray,staytime:double);
top1 = FOREACH (GROUP data BY (imsi,time)){
sorted = ORDER data BY staytime DESC;
top = LIMIT sorted 1;
GENERATE FLATTEN (top);
}



结果截图：
 